# Set vim style
bindkey -v

# Remove older command from the history if a duplicate is to be added.
setopt HIST_IGNORE_ALL_DUPS

WORDCHARS=${WORDCHARS//[\/]}
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)

# Initialize zim

# Define zim location
if [[ $ZIM_HOME/init.zsh -ot $ZDOTDIR/.zimrc ]]; then
  source $ZIM_HOME/zimfw.zsh init -q
fi

source $ZIM_HOME/init.zsh

# Prompt (pure)
# The default prompt symbols don't work well in TTY
PURE_PROMPT_SYMBOL="$"
PURE_PROMPT_VICMD_SYMBOL="<"

# Don't check for untracked files; faster prompt.
PURE_GIT_UNTRACKED_DIRTY=0
PURE_CMD_MAX_EXEC_TIME=1
fpath+=$XDG_CONFIG_HOME/zsh/pure
autoload -U promptinit; promptinit
prompt pure

# Change cursor shape for different vi modes.
function zle-keymap-select {
  if [[ ${KEYMAP} == vicmd ]] ||
     [[ $1 = 'block' ]]; then	
    echo -ne '\e[1 q'	
  elif [[ ${KEYMAP} == main ]] ||	
       [[ ${KEYMAP} == viins ]] ||	
       [[ ${KEYMAP} = '' ]] ||	
       [[ $1 = 'beam' ]]; then	
    echo -ne '\e[5 q'	
  fi	
}

zle -N zle-keymap-select	
zle-line-init() {	
    # zle -K viins # initiate `vi insert` as keymap (can be removed if `bindkey -V` has been set elsewhere)	
    echo -ne "\e[5 q"	
}	

zle -N zle-line-init	
echo -ne '\e[5 q' # Use beam shape cursor on startup.	
preexec() { echo -ne '\e[5 q' ;} # Use beam shape cursor for each new prompt.

# mkdir -p $XDG_CACHE_HOME/zsh/zcompdump-$ZSH_VERSION
# compinit -d $XDG_CACHE_HOME/zsh/zcompdump-$ZSH_VERSION
# alias units=units --history $XDG_DATA_HOME/units_history

# Load everything in ~/.config/zsh/
for file in $XDG_CONFIG_HOME/zsh/source/*; do
  source $file
done

umask 077

if [ -z $DISPLAY ] && [ "$TTY" = "/dev/tty1" ]; then
  exec sway; exit
  # exec startx; exit
fi

# Set up OSC 7 working directory reporting, allows ctrl+shift+n to open a new
# terminal in current directory.
function osc7-pwd() {
    emulate -L zsh # also sets localoptions for us
    setopt extendedglob
    local LC_ALL=C
    printf '\e]7;file://%s%s\e\' $HOST ${PWD//(#m)([^@-Za-z&-;_~])/%${(l:2::0:)$(([##16]#MATCH))}}
}

function chpwd-osc7-pwd() {
    (( ZSH_SUBSHELL )) || osc7-pwd
}

add-zsh-hook -Uz chpwd chpwd-osc7-pwd
source "${XDG_CONFIG_HOME:-$HOME/.config}/asdf-direnv/zshrc"
